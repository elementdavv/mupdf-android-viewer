apply plugin: 'com.android.application'

group = 'net.timelegend.chaka'
version = '1.24.9a.9'

dependencies {
	if (file('../lib/build.gradle').isFile())
		api project(':lib')
	else
		api 'net.timelegend.chaka:viewer:1.24.9a.9'
}

android {
	compileSdkVersion 34
	defaultConfig {
        applicationId 'net.timelegend.chaka.viewer.app'
        namespace 'net.timelegend.chaka.viewer.app'
		minSdkVersion 21
		targetSdkVersion 34
		versionName '1.24.9a.9'
		versionCode 155

        if (!project.hasProperty('noSplits')) {
	        splits {
		        abi {
			        enable true
			        reset()
			        include('arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64')
			        universalApk true
		        }
	        }
        }
        else if (project.hasProperty('ABI_FILTERS')) {
            ndk {
                abiFilters = []
                abiFilters.addAll(ABI_FILTERS.split(',').collect{it as String})
            }
        }

        packagingOptions {
            jniLibs {
                keepDebugSymbols "**/*.so"
            }
        }
	}
    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }
	if (project.hasProperty('release_storeFile')) {
		signingConfigs {
			release {
				enableV3Signing true
				enableV4Signing true
				storeFile file(release_storeFile)
				storePassword release_storePassword
				keyAlias release_keyAlias
				keyPassword release_keyPassword
			}
		}
		buildTypes {
			release {
				signingConfig signingConfigs.release
				ndk {
					debugSymbolLevel 'FULL'
				}
			}
		}
	}
}
